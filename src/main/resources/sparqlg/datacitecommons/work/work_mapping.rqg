PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

GENERATE <work_mapping>(?work_data) {

    ## fill in blanks in work definition in vivo ontology
    GENERATE <https://projects.tib.eu/tapir/vivo-rdf/research.rqg> (?id, ?pubtype, ?title, ?doi, ?pubyear_dtv) .
}
WHERE {
    # basic properties of a publication
    BIND(<https://projects.tib.eu/tapir/datacitecommons/work/work_id.rqg>(?work_data) AS ?id)
    BIND(fun:JSONPath(?work_data, '$.doi' ) AS ?doi)
    BIND(fun:JSONPath(?work_data, '$.titles[0].title' ) AS ?title)

    BIND(fun:JSONPath(?work_data, '$.publicationYear' ) AS ?pubyear)
    BIND(xsd:dateTime(CONCAT(STR(?pubyear), "-01-01T00:00:00"))  AS ?pubyear_dtv)

    BIND(fun:JSONPath(?work_data, '$.types.citeproc' ) AS ?citeproc)
    # mapping taken from VIVO (claim DOI functionality)
    BIND (
     IRI(COALESCE(
       IF(?citeproc = "article-journal", bibo:AcademicArticle, 1/0),
       IF(?citeproc = "article", bibo:Article, 1/0),
       IF(?citeproc = "book", bibo:Book, 1/0),
       IF(?citeproc = "chapter", bibo:Chapter, 1/0),
       IF(?citeproc = "dataset", vivo:Dataset, 1/0),
       IF(?citeproc = "figure", bibo:Image, 1/0),
       IF(?citeproc = "graphic", bibo:Image, 1/0),
       IF(?citeproc = "legal_case", bibo:LegalCaseDocument, 1/0),
       IF(?citeproc = "legislation", bibo:Legislation, 1/0),
       IF(?citeproc = "manuscript", bibo:Manuscript, 1/0),
       IF(?citeproc = "map", bibo:Map, 1/0),
       IF(?citeproc = "musical_score", vivo:Score, 1/0),
       IF(?citeproc = "paper-conference", vivo:ConferencePaper, 1/0),
       IF(?citeproc = "patent", bibo:Patent, 1/0),
       IF(?citeproc = "personal_communication", bibo:PersonalCommunicationDocument, 1/0),
       IF(?citeproc = "post-weblog", vivo:BlogPosting, 1/0),
       IF(?citeproc = "report", bibo:Report, 1/0),
       IF(?citeproc = "review", vivo:Review, 1/0),
       IF(?citeproc = "speech", vivo:Speech, 1/0),
       IF(?citeproc = "thesis", bibo:Thesis, 1/0),
       IF(?citeproc = "webpage", bibo:Webpage, 1/0),

     # mapping leftover types
       IF(STRSTARTS(?citeproc, "article"), bibo:Article, 1/0),
       IF(?citeproc = "bill", bibo:Bill, 1/0),
       IF(STRSTARTS(?citeproc, "entry"), bibo:DocumentPart, 1/0),
       IF(?citeproc = "interview", bibo:Interview, 1/0),
       IF(?citeproc = "motion_picture", bibo:Film, 1/0),
       IF(?citeproc = "post", vivo:BlogPosting, 1/0),
       IF(?citeproc = "review-book", vivo:Review, 1/0),
       IF(?citeproc = "song", bibo:AudioDocument, 1/0),
       IF(?citeproc = "treaty", bibo:LegalDocument, 1/0),

       bibo:Document  #default if nothing else fits
     )) AS ?pubtype
    )
}