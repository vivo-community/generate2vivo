PREFIX fun: <http://w3id.org/sparql-generate/fn/>

GENERATE <person_source>(?orcid) {
    # create person once
    GENERATE <https://projects.tib.eu/tapir/datacitecommons/person/person_mapping.rqg>( ?person_data ) .

    # go into loop
    GENERATE <https://projects.tib.eu/tapir/datacitecommons/personPlusPublications/person2publication.rqg>( ?orcid, ?person_id, ?undef ) .
}
SOURCE <https://projects.tib.eu/tapir/datacitecommons/person/documentset/person.graphql> AS ?graphqlquery
WHERE {
    # replace whitespace in graphql query (compress message)
    BIND(REPLACE(?graphqlquery, "\\s+", " ") AS ?graphql)
    BIND ("""\{"variables":\{"orcid":"{?orcid}"},"query":"{?graphql}"}""" AS ?postbody)

    # build http post request
    BIND( fun:HTTPPost(<https://api.datacite.org/graphql>, "Content-type: application/json" , ?postbody) AS ?response )
    FILTER( fun:HTTPExtractResponseCode( ?response ) = 200 )
    BIND( fun:HTTPExtractBody( ?response ) AS ?body )

    BIND( fun:JSONPath( ?body, "$.errors" ) AS ?errors )
    FILTER( !BOUND(?errors) )

    BIND( fun:JSONPath( ?body, "$.data.person" ) AS ?person_data )
    BIND(<https://projects.tib.eu/tapir/datacitecommons/person/person_id.rqg>(?person_data) AS ?person_id)
}